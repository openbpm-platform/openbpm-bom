/*
 * Copyright (c) Haulmont 2025. All Rights Reserved.
 * Use is subject to license terms.
 */

plugins {
    id 'java-platform'
    id 'maven-publish'
}

repositories {
    if (rootProject.hasProperty('openbpmRepoUrl')) {
        maven {
            url rootProject['openbpmRepoUrl']

            if (rootProject.hasProperty('openbpmRepoUser') && rootProject.hasProperty('openbpmRepoPassword')) {
                credentials {
                    username rootProject['openbpmRepoUser']
                    password rootProject['openbpmRepoPassword']
                }
            }
        }
        mavenCentral()
    } else {
        mavenLocal()
        maven {
            url "https://nexus.openbpm.ru/repository/public"
        }
        mavenCentral()
    }
}

group = 'io.openbpm.bom'

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("org.springframework.cloud:spring-cloud-dependencies:2025.0.0")

    def themeVersion = version
    def uiKitVersion = version

    constraints {
        api "io.openbpm.ui:openbpm-theme:$themeVersion"
        api "io.openbpm.ui:ui-kit:$uiKitVersion"
        api "io.openbpm.ui:openbpm-ui-kit-starter:$uiKitVersion"

        def camundaVersion = '7.23.0'
        api "org.camunda.community.rest:camunda-platform-7-rest-client-spring-boot:$camundaVersion"
        api "io.holunda.camunda-api:camunda-bpm-engine-api:$camundaVersion"

        api "com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3"

        def mapstructVersion = '1.6.3'
        api "org.mapstruct:mapstruct:$mapstructVersion"
        api "org.mapstruct:mapstruct-processor:$mapstructVersion"

        api "org.projectlombok:lombok:1.18.36"

        api "org.vaadin.addons.componentfactory:spinner:24.1.0"

        def testcontainersVersion = '1.20.6'
        api "org.testcontainers:junit-jupiter:$testcontainersVersion"
        api "org.testcontainers:postgresql:$testcontainersVersion"
    }
}

publishing {
    if (rootProject.hasProperty('openbpmUploadUrl')) {
        repositories {
            maven {
                url = rootProject['openbpmUploadUrl']
                credentials {
                    username rootProject['openbpmUploadUser']
                    password rootProject['openbpmUploadPassword']
                }
                allowInsecureProtocol = true
            }
        }
    }

    publications {
        javaPlatformMaven(MavenPublication) {
            artifactId = 'openbpm-bom'
            from components.javaPlatform
        }
    }
}